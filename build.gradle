buildscript {
    repositories {
        mavenCentral()
        maven {
            url = 'https://repo.grails.org/grails/restricted'
        }
    }
    dependencies { // Not Published to Gradle Plugin Portal
        classpath platform("org.apache.grails:grails-bom:$grailsVersion")
        classpath "org.apache.grails:grails-data-hibernate5"
        classpath "org.apache.grails:grails-gradle-plugins"
    }
}

plugins {
    id "war"
    id "idea"
    id "eclipse"
    id "com.jfrog.artifactory" version "5.2.5"
    id 'maven-publish'
}

// Not Published to Gradle Plugin Portal
apply plugin: "org.apache.grails.gradle.grails-plugin"

group = "org.apache.grails.plugins"

repositories {
    mavenCentral()
    maven {
        url = 'https://repo.grails.org/grails/restricted'
    }
}

dependencies {
    profile "org.apache.grails.profiles:plugin"
    developmentOnly "org.springframework.boot:spring-boot-devtools"
    // Spring Boot DevTools may cause performance slowdowns or compatibility issues on larger applications
    implementation platform("org.apache.grails:grails-bom:$grailsVersion")
    implementation "org.apache.grails:grails-core"
    implementation "org.apache.grails:grails-data-hibernate5"
    implementation "org.apache.grails:grails-databinding"
    implementation "org.apache.grails:grails-events"
    implementation "org.apache.grails:grails-interceptors"
    implementation "org.apache.grails:grails-logging"
    implementation "org.apache.grails:grails-rest-transforms"
    implementation "org.apache.grails:grails-services"
    implementation "org.apache.grails:grails-url-mappings"
    implementation "org.apache.grails:grails-web-boot"
    implementation "org.springframework.boot:spring-boot-autoconfigure"
    implementation "org.springframework.boot:spring-boot-starter-logging"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    console "org.apache.grails:grails-console"
    runtimeOnly "com.h2database:h2"
    runtimeOnly "com.zaxxer:HikariCP"
    testImplementation "org.apache.grails:grails-testing-support-datamapping"
    testImplementation "org.apache.grails:grails-testing-support-web"
    testImplementation "org.spockframework:spock-core"
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.18.0'
}

compileJava.options.release = 17

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('sourceJar', Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allJava
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.sourceJar
        }
    }
}

artifactory {
    contextUrl = 'https://artifactory.abz-reporting.com/artifactory'
    publish {
        repository {
            repoKey = version.contains('SNAPSHOT') ? 'plugins-snapshot-local' : 'plugins-release-local'
            username = "${abzLdapUser}"
            password = "${abzLdapPassword}"
        }
        defaults {
            publications('mavenJava')
        }
    }

}


